buildscript {
    repositories {
        maven { url "http://oss.jfrog.org/artifactory/repo"}
        jcenter()
    }
    dependencies {
        classpath 'io.ratpack:ratpack-gradle:0.9.19'
    }
}

// The “ratpack” plugin applies the “application” plugin, making it easy to create a standalone application.
// See: http://gradle.org/docs/current/userguide/application_plugin.html
apply plugin: "io.ratpack.ratpack-groovy"
//apply plugin:"clojure"
// The “ratpack” plugin is IDEA aware.
// It will create a run configuration in IDEA to launch your app in your IDE, with hot reloading.
apply plugin: "idea"
idea {
    project {
        jdkName "1.8"
        languageLevel "1.8"
        ipr {
            withXml { provider ->
                def node = provider.asNode()
                //configure git support for the project in idea
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'
            }
        }
    }
}


repositories {
  maven { url "http://oss.jfrog.org/artifactory/repo" }

  jcenter()
  maven { url "http://repo.springsource.org/repo" } // for springloaded

}

configurations.all {
    exclude module: "groovy"
}

dependencies {
    // SpringLoaded enables runtime hot reloading.
    // It is not part of the app runtime and is not shipped in the distribution.
    // springloaded "org.springframework:springloaded:1.2.1.RELEASE"

    // Default SLF4J binding. Note that this is a blocking implementation.
    // See here for a non blocking appender http://logging.apache.org/log4j/2.x/manual/async.html
    runtime 'org.slf4j:slf4j-simple:1.7.7'

    testCompile "org.spockframework:spock-core:0.7-groovy-2.0", {
        exclude module: "groovy-all"
    }
}
//download the cljs jar to compile our clojure sources if it is not already in our libs directory
task downloadClj <<{
  def cljsjar = new File("libs/cljs.jar")
  if(!cljsjar.exists()){
    ant.get(src: 'https://github.com/clojure/clojurescript/releases/download/r1.7.28/cljs.jar',
    dest: 'libs',
    skipexisting: 'true')
  }
 }

//we need to compile the cljs sources using the configurations in build.clj
task compileCljs(type: Exec){
  dependsOn downloadClj
  executable "sh"
  args "-c", "java -cp libs/cljs.jar:src/main/clojurescript clojure.main build.clj"

}
//lets hook the compilation of cljs into the java compilation by running it before compileJava
compileJava.dependsOn(compileCljs)
